## customize prompt
zstyle ':prompt:grml:left:setup' items rc path kubectl-context vcs percent
zstyle ':prompt:grml:right:setup' use-rprompt false

## add kubectl-context grml token
grml_theme_add_token kubectl-context -f kubectl_context '%F{magenta}(%F{blue}' '%F{magenta})%f '

## customize vcs_info
zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' unstagedstr '!'
zstyle ':vcs_info:*' stagedstr '+'
zstyle ':vcs_info:git*' formats "%F{magenta}(%F{blue}%b%F{yellow}%m%u%c%F{magenta})%f " "zsh: %r"

## customize path/change-root
zstyle ':prompt:grml:*:items:path' pre '%F{cyan}'
zstyle ':prompt:grml:*:items:path' token ' %35<..<%~%<< '
zstyle ':prompt:grml:*:items:path' post '%f'

## add $HOME/.bin to $PATH (remove this)
export PATH=$HOME/.bin:$HOME:$PATH

## Aliases
[[ -f .zshrc.aliases ]] && source .zshrc.aliases

## kubectl functions
function kubectl_context () {
  NAMESPACE=$(kubectl config view --minify --output 'jsonpath={..namespace}')
  CONTEXT=$(kubectl config view --minify --output 'jsonpath={..current-context}')
  REPLY="${CONTEXT}|${NAMESPACE}"
}

function nctx () {
  kubectl config set-context --current --namespace=$1
}

function kctx () {
  kubectl config use-context $1
}

## Extra functions
function ssh-agent () {
    [[ -S $SSH_AUTH_SOCK ]] && return
    if [[ -f ~/.agent.$HOST ]] ; then # existing agent data, try it and break if it works
        eval `cat ~/.agent.$HOST`
        ps -o "user,command" -p $SSH_AGENT_PID | tail -1 | egrep -q "^${USER}.*ssh-agent\$" && return
    fi
    # else create new agent
    command ssh-agent | grep -v echo > ~/.agent.$HOST
    eval `cat ~/.agent.$HOST`
}

function ssh_aws {
  SSH_BIN=$(command which ssh)
  AWS_IP=$( echo $@ | grep -oE 'ip-.*.ec2.internal' )

  if [ ! -z ${AWS_IP} ] ; then
    REAL_IP=$( echo ${AWS_IP%%.*} | sed -e 's/ip-//' -e 's/-/./g' )
    ${SSH_BIN} $( echo $@ | sed -e "s/${AWS_IP}/${REAL_IP}/g" )
  else
    ${SSH_BIN} $@
  fi
}

## functions at new sessions
ssh-agent
